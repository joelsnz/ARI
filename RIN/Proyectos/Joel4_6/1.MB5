1 '----------------------------------------------------------------'
2 'Definimos variables de posicionamiento para los puntos del palet'
3 'como son las cuatro esquinas del mismo '
4 '----------------------------------------------------------------'
5 DEF POS P101
6 DEF POS P102
7 DEF POS P103
8 DEF POS P201
9 DEF POS P202
10 DEF POS P203
11 def pos altura1
12 def pos altura2
13 '----------------------------------------------------------------'
14 'Definimos una variable de posición para una distancia '
15 'de seguridad, desde la cual realizaremos las operaciones MVS '
16 '----------------------------------------------------------------'
17 DEF POS VecZ25
18 DEF POS VecZ22
19 '----------------------------------------------------------------'
20 'Definimos variables enteras para poder realizar el bucle '
21 'while / wend para el total de las piezas a paletizar. '
22 '----------------------------------------------------------------'
23 DEF INTE Piezas1
24 DEF INTE Piezas2
25 def inte isalt1
26 def inte isalt2
27 '----------------------------------------------------------------'
28 'Definimos la variable para usarla en el bucle para while/wend '
29 'asignando el palet y el contador de piezas '
30 '----------------------------------------------------------------'
31 DEF POS P70
32 '----------------------------------------------------------------'
33 'Asignamos valores a cada una de las posiciones de las esquinas '
34 'del palet y a la distancia de seguridad '
35 '----------------------------------------------------------------'
36 P101 = P1Pal + (55,0,0,0,0,0)
37 P102 = P1Pal + (0,-55,0,0,0,0)
38 P103 = P1Pal + (55,-55,0,0,0,0)
39 P201 = P2Pal + (55,0,0,0,0,0)
40 P202 = P2Pal + (0,-110,0,0,0,0)
41 P203 = P2Pal + (55,-110,0,0,0,0)
42 VecZ25 = (0,0,+25,0,0,0)
43 VecZ22 = (0,0,+22,0,0,0)
44 '----------------------------------------------------------------'
45 'Definimos la entrada de control del programa como detector de '
46 'pieza al final de rampa. Dado por el bit 6 '
47 '----------------------------------------------------------------'
48 DEF IO COLOR = BIT, 900
49 DEF IO PART_AV = BIT,6
50 def io PM = bit, 3
51 def io PR = bit, 5
52 def io Q1 = bit, 2
53 def io Q2 = bit, 3

54 OVRD 100
55 SPD 500
56 '----------------------------------------------------------------'
57 'Definimos el palet. '
58 '----------------------------------------------------------------'
59 DEF PLT 1, P1Pal,P101,P102,P103,2,2,1
60 DEF PLT 2, P2Pal,P201,P202,P203,2,3,1

61 altura1 = (0,0,0,0,0,0)
62 altura2 = (0,0,0,0,0,0)
63 Piezas1 = 1
64 Piezas2 = 1
65 isalt1 = 0
66 isalt2 = 0

67 mov PSec
68 hopen 1

69 *Inicio

70 while PM = 0
71 wend

72 *PieceSelect
73 while PART_AV = 0
74 wend

75 mov PColor

76 if COLOR = 1then
77 	if Piezas1 <=4 then
78 		P70 = (PLT 1, Piezas1) + altura1
79 		Piezas1 = Piezas1 + 1
80 	else 
81 		Piezas1 = 1
82 		isalt1 = 1
83 		altura1 = altura1 + VecZ22
84 		goto *PieceSelect
85 	endif
86 else
87 	if Piezas2 <= 6 then
88 		P70 = (PLT 2, Piezas2) + altura2
89 		Piezas2 = Piezas2 + 1
90 	else
91 		Piezas2 = 1
92 		isalt2 = 1
93 		altura2 = altura2 + VecZ25
94 		goto *PieceSelect
95 	endif
96 endif

97 mvr PColor, PInColor, PIn + VecZ25
98 mvs PIn
99 hclose 1
100 dly 0.5
101 mvs PIn + VecZ25
102 mov PSec
103 mov P70 + VecZ25
104 mvs P70
105 hopen 1
106 dly 0.5
107 mvs P70 + VecZ25
108 mov PSec

109 if Piezas1 = 5and isalt1 = 1 then
110 	Q1 = 1
111 	while PR = 0
112 	wend
113 	Q1 = 0
114 	altura1 = (0,0,0,0,0,0)
115 	Piezas1 = 1
116 	isalt1 = 0
117 	goto *Inicio
118 endif

119 if Piezas2= 7 and isalt2 = 1 then
120 	Q2 = 1
121 	while PR = 0
122 	wend
123 	Q2 = 0
124 	altura2 = (0,0,0,0,0,0)
125 	Piezas2 = 1
126 	isalt2 = 0
127 	goto *Inicio
128 endif

129 goto *PieceSelect

130 goto *Inicio
131 End
